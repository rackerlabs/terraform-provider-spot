---
# generated by https://github.com/hashicorp/terraform-plugin-docs
page_title: "Terraform Provider for Rackspace Spot"
subcategory: ""
description: |-
  The rackspace spot terraform provider offers a streamlined solution for creating and managing cloudspaces on Rackspace's robust infrastructure.
---

# Rackspace Spot Provider

The Rackspace Spot Provider is a solution for creating and managing multiple cloudspaces, on Rackspace's robust infrastructure.

Rackspace Spot delivers fully managed and pre-configured Kubernetes clusters, known as cloudspaces, providing a turnkey solution for deploying and running containerized applications.

## Authenticating with Rackspace Spot

To use this provider, set an authentication token as an environment variable, obtainable via the Rackspace Spot dashboard, https://spot.rackspace.com.

- `RXTSPOT_TOKEN`:  This is the actual token value.
- `RXTSPOT_TOKEN_FILE`: This is the absolute path to the file containing the token value.

```bash
export RXTSPOT_TOKEN=<rackspace-spot-token>
# or
export RXTSPOT_TOKEN_FILE=/path/to/token/file
```
You can set the token directly in the provider configuration as shown below. However, it is recommended to use Terraform variables instead of hard-coding the token in your Terraform configuration files. This approach prevents accidental exposure of the token in version control.

## Example Usage

Declare the provider in your configuration and terraform init will automatically fetch and install the provider for you from the Terraform Registry:

```terraform
terraform {
  required_providers {
    spot = {
      source = "rackerlabs/spot"
    }
  }
}

provider "spot" {
  # the provider configuration overrides environment variables
  rxtspot_token = "<token>"
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Optional

- `rxtspot_token` (String, Sensitive) API token used to authenticate against Spot backend

## Create Your First Cloudspace

Get started with Rackspace Spot by creating your first Spot Cloudspace. Follow the steps below to create a cloudspace and deploy your workloads.

1. Log in to the [Rackspace Spot Console](https://spot.rackspace.com) and copy the Access Token:
   - Navigate to the **Terraform** menu under **API Access** on the left pane.
   - Copy the **Access Token** provided on that page.
   - **Important:** Treat the access token as sensitive information. Avoid sharing it publicly.

2. Set the copied token as an environment variable:

   ```bash
   echo "<your_access_token>" > ~/.rxtspot_token
   export RXTSPOT_TOKEN_FILE=$HOME/.rxtspot_token
   ```

3. Create your terraform configuration file

```terraform
terraform {
  required_providers {
    spot = {
      source  = "rackerlabs/spot"
      version = "0.0.3"
    }
  }
}

provider "spot" {}

# Example of cloudspace resource.
resource "spot_cloudspace" "example" {
  cloudspace_name    = "name your cloudspace"
  region             = "us-central-dfw-1"
  hacontrol_plane    = false
  preemption_webhook = "https://hooks.slack.com/services/T00000000/B00000000/XXXXXXXXXXXXXXXXXXXXXXXX"
}

# Creates a spot node pool with two servers of class gp.vs1.medium-dfw.
resource "spot_spotnodepool" "non-autoscaling-bid" {
  cloudspace_name      = resource.spot_cloudspace.example.cloudspace_name
  server_class         = "gp.vs1.medium-dfw"
  bid_price            = 0.008
  desired_server_count = 2
}

# Creates a spot node pool with an autoscaling pool of 3-8 servers of class gp.vs1.large-dfw.
resource "spot_spotnodepool" "autoscaling-bid" {
  cloudspace_name = resource.spot_cloudspace.example.cloudspace_name
  server_class    = "gp.vs1.large-dfw"
  bid_price       = 0.012

  autoscaling = {
    min_nodes = 3
    max_nodes = 8
  }
}

data "spot_cloudspace" "example" {
  id = resource.spot_cloudspace.example.id
}

output "kubeconfig" {
  value = data.spot_cloudspace.example.kubeconfig
}
```

4. Use generated kubeconfig to access your cloudspace.
   - The cloudspace phase must be healthy

## Know your Cloudspace

A Cloudspace represents a logical unit of your cloud infrastructure. It includes:

- An infrastructure pool comprising servers, storage and network. Rackspace Spot uses spare virtual server capacity from Rackspace's global data-centers as the backing infrastructure
- A cloud operating system such as Kubernetes

Learn more about Cloudspaces [here](https://spot.rackspace.com/docs/create-rackspace-spot-cloudspace).

### Spotnodepool

A spotnodepool is a pool of nodes in a Spot cloudspace. In Spot, you organize your servers into groups called spotnodepools, which you add to your cloudspace. A spotnodepool is a collection of servers with identical specifications. You can define multiple spotnodepools within a single cloudspace, and all these server groups will be part of the same Kubernetes cluster associated with that cloudspace. The servers in a spotnodepool become worker nodes, in terms of Kubernetes cluster terminology.

```terraform
resource "spot_spotnodepool" "example" {
  cloudspace_name      = "example"
  server_class         = "gp.vs1.small-dfw"
  bid_price            = 0.002
  desired_server_count = 2
}
```

### Spotnodepool with Autoscaling

A spotnodepool can be configured to dynamically adjust the number of servers based on demand. This is known as autoscaling. When demand is high, the spotnodepool can automatically increase the number of servers, and when demand decreases, it can scale down to optimize costs. You can specify the minimum and maximum number of servers in the spotnodepool, and the spotnodepool will automatically scale up or down based on the load.

```terraform
# Creates a spot node pool with a two servers of class gp.vs1.small-dfw and autoscaling enabled.
# If load increases, the number of nodes will increase up to 4 from the minimum of 2.
resource "spot_spotnodepool" "example" {
  cloudspace_name = "example"
  server_class    = "gp.vs1.small-dfw"
  bid_price       = 0.002
  autoscaling = {
    min_nodes = 2
    max_nodes = 4
  }
}
```
