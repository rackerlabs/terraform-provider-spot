---
# generated by https://github.com/hashicorp/terraform-plugin-docs
page_title: "Terraform Provider for Rackspace Spot"
subcategory: ""
description: |-
  The rackspace spot terraform provider offers a streamlined solution for creating and managing cloudspaces on Rackspace's robust infrastructure.
---

# Rackspace Spot Provider

The Rackspace Spot Provider is a solution for creating and managing multiple cloudspaces, on Rackspace's robust infrastructure.

Rackspace Spot delivers fully managed and pre-configured Kubernetes clusters, known as cloudspaces, providing a turnkey solution for deploying and running containerized applications.

## Authenticating with Rackspace Spot

To use this provider, you will require an authentication token. You can obtain the token from the Rackspace Spot dashboard at https://spot.rackspace.com by navigating to the "API Access" section located on the left sidebar.

### Setting the Token in the Provider Configuration

You can provide the authentication token in the provider configuration using the `token` attribute. It is recommended to use Terraform variables or other secure methods to store and reference the token value, rather than hard-coding it directly in your Terraform configuration files. This helps prevent accidental exposure of the token in version control systems.

Example using a Terraform variable:

```terraform
variable "rackspace_spot_token" {
  description = "Rackspace Spot authentication token"
  type        = string
  sensitive   = true
}

provider "spot" {
  token = var.rackspace_spot_token
}
```

### Setting the Token using Environment Variables

Alternatively, you can set the authentication token using environment variables:

- `RXTSPOT_TOKEN`: Set this variable to the actual token value.
- `RXTSPOT_TOKEN_FILE`: Set this variable to the absolute path of the file containing the token value.

Example:

```shell
export RXTSPOT_TOKEN="your-rackspace-spot-token"
# or
export RXTSPOT_TOKEN_FILE="/path/to/token/file"
```

Remember to keep your authentication token secure and avoid committing it to version control systems. Use appropriate security measures, such as encrypting sensitive files or using secret management tools, to protect your token.

## Example Usage

Declare the provider in your configuration and terraform init will automatically fetch and install the provider for you from the Terraform Registry:

```terraform
terraform {
  required_providers {
    spot = {
      source = "rackerlabs/spot"
    }
  }
}

provider "spot" {
  token = "<rxtspot_token>"
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Optional

- `token` (String, Sensitive) API token used to authenticate against Spot backend

## Create Your First Cloudspace

Get started with Rackspace Spot by creating your first Spot Cloudspace. Follow the steps below to create a cloudspace and deploy your workloads.

1. Log in to the [Rackspace Spot Console](https://spot.rackspace.com) and copy the Access Token:
   - Navigate to the **Terraform** menu under **API Access** on the left pane.
   - Copy the **Access Token** provided on that page.
   - **Important:** Treat the access token as sensitive information. Avoid sharing it publicly.

2. Create your terraform configuration file

```terraform
terraform {
  required_providers {
    spot = {
      source = "rackerlabs/spot"
    }
  }
}

variable "rackspace_spot_token" {
  description = "Rackspace Spot authentication token"
  type        = string
  sensitive   = true
}

provider "spot" {
  token = var.rackspace_spot_token
}

# Example of cloudspace resource.
resource "spot_cloudspace" "example" {
  cloudspace_name    = "name your cloudspace"
  region             = "us-central-dfw-1"
  hacontrol_plane    = false
  preemption_webhook = "https://hooks.slack.com/services/T00000000/B00000000/XXXXXXXXXXXXXXXXXXXXXXXX"
}

# Creates a spot node pool with two servers of class gp.vs1.medium-dfw.
resource "spot_spotnodepool" "non-autoscaling-bid" {
  cloudspace_name      = resource.spot_cloudspace.example.cloudspace_name
  server_class         = "gp.vs1.medium-dfw"
  bid_price            = 0.008
  desired_server_count = 2
}

# Creates a spot node pool with an autoscaling pool of 3-8 servers of class gp.vs1.large-dfw.
resource "spot_spotnodepool" "autoscaling-bid" {
  cloudspace_name = resource.spot_cloudspace.example.cloudspace_name
  server_class    = "gp.vs1.large-dfw"
  bid_price       = 0.012

  autoscaling = {
    min_nodes = 3
    max_nodes = 8
  }
}

data "spot_cloudspace" "example" {
  id = resource.spot_cloudspace.example.id
}

output "kubeconfig" {
  value = data.spot_cloudspace.example.kubeconfig
}
```

4. Use generated kubeconfig to access your cloudspace.

## Know your Cloudspace

A Cloudspace represents a logical unit of your cloud infrastructure. It includes:

- An infrastructure pool comprising servers, storage and network. Rackspace Spot uses spare virtual server capacity from Rackspace's global data-centers as the backing infrastructure
- A cloud operating system such as Kubernetes

Learn more about Cloudspaces [here](https://spot.rackspace.com/docs/create-rackspace-spot-cloudspace).

### Spotnodepool

A spotnodepool is a pool of nodes in a Spot cloudspace. In Spot, you organize your servers into groups called spotnodepools, which you add to your cloudspace. A spotnodepool is a collection of servers with identical specifications. You can define multiple spotnodepools within a single cloudspace, and all these server groups will be part of the same Kubernetes cluster associated with that cloudspace. The servers in a spotnodepool become worker nodes, in terms of Kubernetes cluster terminology.

```terraform
resource "spot_spotnodepool" "example" {
  cloudspace_name      = "example"
  server_class         = "gp.vs1.small-dfw"
  bid_price            = 0.002
  desired_server_count = 2
}
```

### Spotnodepool with Autoscaling

A spotnodepool can be configured to dynamically adjust the number of servers based on demand. This is known as autoscaling. When demand is high, the spotnodepool can automatically increase the number of servers, and when demand decreases, it can scale down to optimize costs. You can specify the minimum and maximum number of servers in the spotnodepool, and the spotnodepool will automatically scale up or down based on the load.

```terraform
# Creates a spot node pool with a two servers of class gp.vs1.small-dfw and autoscaling enabled.
# If load increases, the number of nodes will increase up to 4 from the minimum of 2.
resource "spot_spotnodepool" "example" {
  cloudspace_name = "example"
  server_class    = "gp.vs1.small-dfw"
  bid_price       = 0.002
  autoscaling = {
    min_nodes = 2
    max_nodes = 4
  }
}
```
